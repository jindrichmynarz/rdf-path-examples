PREFIX rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:  <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rpath: <https://w3id.org/lodsight/rdf-path#>
PREFIX spin:  <http://spinrdf.org/spin#>

CONSTRUCT {
  [] a spin:ConstraintViolation ;
    spin:violationRoot ?edge ;
    spin:violationPath ?property ;
    spin:violationValue ?value ;
    rdfs:label "Edge starts and ends must have a type."@en .
}
WHERE {
  VALUES ?property {
    rpath:start
    rpath:end
  }
  [] rpath:edges/rdf:rest*/rdf:first ?edge .
  ?edge ?property ?value .
  FILTER NOT EXISTS {
    ?value a ?type .
  }
}
